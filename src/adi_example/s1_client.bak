(ns adi-example.s1-client
  (:require [vraja.cljs :refer [js-ch]]
            [cljs.core.async :refer [<! >! put! close! timeout]]
            [dommy.core :as d])
  (:require-macros [cljs.core.async.macros :refer [go]]
                   [dommy.macros :refer [node sel1]]))

(defn render-page [bind-input! bind-list!]
  (node
   (list
    [:div
     [:h3 "Send a message to the server:"]
     (doto (node [:input {:type :text :size 50}])
       bind-input!)]
    [:div
     [:h3 "Messages from the server:"]
     (doto (node [:div])
       bind-list!)])))

(defn render-list [msgs]
  (node
   [:ul
    (if (seq msgs)
      (for [msg msgs]
        [:li (pr-str msg)])
      [:li "None yet."])]))

(defn list-binder [msgs]
  (fn [$list]
    (add-watch msgs ::list
               (fn [_ _ _ msgs]
                 (->> (reverse msgs)
                      (take 10)
                      (render-list)
                      (d/replace-contents! $list))))))

(defn input-binder [ch]
  (fn [$input]
    (d/listen! $input :keyup
               (fn [e]
                 (when (= 13 (.-keyCode e))
                   (put! ch (js/JSON.parse (d/value $input)))
                   (d/set-value! $input ""))))))

(defn bind-msgs [ch msgs]
  (go
   (loop []
     (when-let [msg (<! ch)]
       
       (js/console.log msg)
       (swap! msgs conj msg)
       (recur)))))

(set! (.-onload js/window)
      (fn []
        (go
         (let [msgs (atom [])
               ws (<! (js-ch "ws://localhost:8088/ws"))]
           (bind-msgs ws msgs)
           (d/replace-contents! (sel1 :#content)
                                (render-page (input-binder ws)
                                             (list-binder msgs)))
           (reset! msgs [])))))




